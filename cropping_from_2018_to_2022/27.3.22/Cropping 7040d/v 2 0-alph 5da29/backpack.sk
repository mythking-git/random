command /backpack:
	trigger:
		# OPEN BACKPACK
		set metadata tag "backpack" of player to chest inventory with 3 rows named "&7&lBackpack - P%formatNum({%player's uuid%::prestige})% - L%formatNum({%player's uuid%::level})%"
		wait 1 tick

		# FORMATTING BACKGROUND
		loop 10 times:
			set slot loop-value - 1 of metadata tag "backpack" of player to gray stained glass pane
		loop 10 times:
			set slot (loop-value + 16) of metadata tag "backpack" of player to gray stained glass pane
			set slot 22 of metadata tag "backpack" of player to barrier named "&8--| &4&lExit &8|-- "

		# BACKPACK UPGRADE
		set {%player's uuid%::collected} to {%player's uuid%::wheat} + {%player's uuid%::carrot} + {%player's uuid%::potato} + {%player's uuid%::cactus} + {%player's uuid%::pumpkin} + {%player's uuid%::melon}
		set {_levelMulti} to ({%player's uuid%::prestige}/2)
		set {_prestigeMulti} to (0.05 * {%player's uuid%::level})
		set {%player's uuid%::multi} to 1 + ({_levelMulti} + {_prestigeMulti})
		set {_wheat} to (({%player's uuid%::wheat} * 1) * {%player's uuid%::multi})
		set {_carrot} to (({%player's uuid%::carrot} * 5) * {%player's uuid%::multi})
		set {_potato} to (({%player's uuid%::potato} * 15) * {%player's uuid%::multi})
		set {_cactus} to (({%player's uuid%::cactus} * 50) * {%player's uuid%::multi})
		set {_pumpkin} to (({%player's uuid%::pumpkin} * 250) * {%player's uuid%::multi})
		set {_melon} to (({%player's uuid%::melon} * 500) * {%player's uuid%::multi})
		set {_baseValue} to ({%player's uuid%::wheat} * 1) + ({%player's uuid%::carrot} * 5) + ({%player's uuid%::potato} * 15) + ({%player's uuid%::cactus} * 50) + ({%player's uuid%::pumpkin} * 250) + ({%player's uuid%::melon} * 500)
		set {_sellValue} to {_wheat} + {_carrot} + {_potato} + {_cactus} + {_pumpkin} + {_melon}
		
		set slot 13 of metadata tag "backpack" of player to trapped chest named "&6&lUpgrade&8&l: " with lore "<##E6BB18>Backpack storage&8&l: <##018080>%formatNum({%player's uuid%::collected})% / %formatNum({%player's uuid%::backpackStorage})%" and "&c&oClick to open upgrade menu."

		# WHEAT
		set slot 10 of metadata tag "backpack" of player to wheat named "&6&lWheat&8&l: " with lore "<##E6BB18>Balance&8&l: <##018080>%formatNum({%player's uuid%::wheat})% wheat" and "<##E6BB18>Base Value&8&l: <##018080>$%formatNum({%player's uuid%::wheat}*1)%" and "<##E6BB18>Worth&8&l: <##018080>$%formatNum({_wheat})%"

		# CARROTS
		if {%player's uuid%::level} >= 10:
			set slot 11 of metadata tag "backpack" of player to carrot named "&6&lCarrots&8&l: " with lore "<##E6BB18>Balance&8&l: <##018080>%formatNum({%player's uuid%::carrot})% carrots" and "<##E6BB18>Base Value&8&l: <##018080>$%formatNum({%player's uuid%::carrot}*5)%" and "<##E6BB18>Worth&8&l: <##018080>$%formatNum({_carrot})%"
		else:
			set slot 11 of metadata tag "backpack" of player to wither rose named "&8--| &4&lLocked &8|-- " with lore "&cReach level 10 to unlock carrots."

		# POTATO
		if {%player's uuid%::level} >= 25:
			set slot 12 of metadata tag "backpack" of player to potato named "&6&lPotato&8&l: " with lore "<##E6BB18>Balance&8&l: <##018080>%formatNum({%player's uuid%::potato})% potato" and "<##E6BB18>Base Value&8&l: <##018080>$%formatNum({%player's uuid%::potato}*15)%" and "<##E6BB18>Worth&8&l: <##018080>$%formatNum({_potato})%"
		else:
			set slot 12 of metadata tag "backpack" of player to wither rose named "&8--| &4&lLocked &8|-- " with lore "&cReach level 25 to unlock potatos."

		# CACTUS
		if {%player's uuid%::level} >= 50:
			set slot 14 of metadata tag "backpack" of player to cactus named "&6&lCacti&8&l: " with lore "<##E6BB18>Balance&8&l: <##018080>%formatNum({%player's uuid%::cactus})% cacti" and "<##E6BB18>Base Value&8&l: <##018080>$%formatNum({%player's uuid%::cactus}*50)%" and "<##E6BB18>Worth&8&l: <##018080>$%formatNum({_cactus})%"
		else:
			set slot 14 of metadata tag "backpack" of player to wither rose named "&8--| &4&lLocked &8|-- " with lore "&cReach level 50 to unlock cacti."

		# PUMPKIN
		if {%player's uuid%::level} >= 90:
			set slot 15 of metadata tag "backpack" of player to pumpkin named "&6&lPumpkin&8&l: " with lore "<##E6BB18>Balance&8&l: <##018080>%formatNum({%player's uuid%::pumpkin})% pumpkins" and "<##E6BB18>Base Value&8&l: <##018080>$%formatNum({%player's uuid%::pumpkin}*250)%" and "<##E6BB18>Worth&8&l: <##018080>$%formatNum({_pumpkin})%"
		else:
			set slot 15 of metadata tag "backpack" of player to wither rose named "&8--| &4&lLocked &8|-- " with lore "&cReach level 90 to unlock pumpkin."

		# MELON
		if {%player's uuid%::level} >= 150:
			set slot 16 of metadata tag "backpack" of player to melon named "&6&lMelon&8&l: " with lore "<##E6BB18>Balance&8&l: <##018080>%formatNum({%player's uuid%::melon})% melons" and "<##E6BB18>Base Value&8&l: <##018080>$%formatNum({%player's uuid%::melon}*500)%" and "<##E6BB18>Worth&8&l: <##018080>$%formatNum({_melon})%"
		else:
			set slot 16 of metadata tag "backpack" of player to wither rose named "&8--| &4&lLocked &8|-- " with lore "&cReach level 150 to unlock melon."
			
		if {_sellValue} > 0:	
			set slot 4 of metadata tag "backpack" of player to shiny golden ingot named "&6&lSell Value: " with lore "<##E6BB18>Base Value: <##018080>$%formatNum({_baseValue})%" and "<##E6BB18>Value&8&l: <##018080>$%formatNum({_sellValue})%" and "&8&oMultiplier: %formatNum({%player's uuid%::multi})%x" and "&c&oClick to sell backpack."
		else:
			set slot 4 of metadata tag "backpack" of player to golden nugget named "&6&lSell Value: " with lore "<##E6BB18>Base Value: <##018080>$%formatNum({_baseValue})%" and "<##E6BB18>Value&8&l: <##018080>$%formatNum({_sellValue})%" and "&8&oMultiplier: %formatNum({%player's uuid%::multi})%x"

		open (metadata tag "backpack" of player) to player

on rightclick:
	if player's held item is chest named "&7&lBackpack":
		cancel event
		execute player command "backpack"

on inventory click:
	if player's gamemode is not creative:
		if event-item is chest:
			cancel event
			execute player command "backpack"
		if event-inventory = (metadata tag "backpack" of player):
			cancel event
			if event-item is trapped chest:
				execute player command "bpupgrade"
			if index of event-slot is 22:
				close inventory of player
			if event-item is golden ingot:
				close inventory of player
				execute player command "sell"
				
on swap hand items:
	event-player's tool is not air
	cancel event
	
	
	
	